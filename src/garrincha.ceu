#include "media/media.ceu"

var int scene_width = 1080;
var int scene_height = 720;

var Region frame = val Region (
                        (0.05 * scene_width) as int,
                        (0.067 * scene_height) as int,
                        3,
                        (0.185 * scene_width) as int,
                        (0.185 * scene_height) as int,
                       );

var Media.Image back = val Media.Image (
                            [].."resources/background.png",
                            Region(0, 0, 1, scene_width, scene_height),
                            1.0,
                           );

var Media.Video video_anim = val Media.Video (
                            [].."resources/animGar.mp4",
                            Region(0, 0, 2, scene_width, scene_height),
                            1.0,
                            1.0,
                           );

var Media.Audio choro = val Media.Audio([].."resources/choro.mp4", 1.0);

var Media.Video drible = val Media.Video (
                                [].."resources/drible.mp4",
                                frame,
                                1.0,
                                1.0,
                             );

var Media.Image img_photo = val Media.Image([].."resources/photo.png", frame, 0.6);

var Media.Image icon = val Media.Image (
                            [].."resources/icon.png",
                            Region(
                                (0.875 * scene_width) as int,
                                (0.117 * scene_height) as int,
                                3,
                                (0.0845 * scene_width) as int,
                                (0.067 * scene_height) as int
                            ),
                            1.0,
                           );

var Media.Video shoes = val Media.Video (
                                [].."resources/shoes.mp4",
                                Region(
                                    (0.15 * scene_width) as int,
                                    (0.6 * scene_height) as int,
                                    3,
                                    (0.25 * scene_width) as int,
                                    (0.25 * scene_height) as int
                                ),
                                1.0,
                                1.0,
                            );

event (int, int, int, int) resize;

var&? Scene s = spawn Scene(Size(scene_width,scene_height),true);
watching s do
    spawn Play(&s.scene, &back);
    var&? Play anim = spawn Play(&s.scene, &video_anim);
    watching anim do
        par do
            var int x;
            var int y;
            var int width;
            var int height;
            every (x, y, width, height) in resize do
                call Play_Set_Region(&anim.play,
                                     Region(x, y, video_anim.region.z, width, height));
            end
        with
            await 5s;
            await Play(&s.scene, &choro);
        with
            await 12s;
            await Play(&s.scene, &drible);
        with
            await 41s;
            var&? Play photo = spawn Play(&s.scene, &img_photo);
            watching photo do
                watching 5s do
                    await 1s;
                    var int top = call Play_Get_Int(&photo.play,"y");
                    var int diff = 290 - top;
                    var int amount = 1;
                    watching 3s do
                        every ((3000.0/diff) as int)ms do
                            call Play_Set_Int(&photo.play, "y", top + amount);
                            amount = amount + 1;
                        end
                    end
                end
            end
        with
            await 45s;
            var bool clicked = do
                watching 6s do
                    spawn Play(&s.scene, &icon);
                    var _char_ptr_ext key;
                    (key,_) = await s.scene.ok_keyed
                              until _strcmp(key,"F1") == 0;
                    escape true;
                end
                escape false;
            end;

            if clicked then
                emit resize (
                        (0.05   * scene_width)  as int,
                        (0.0667 * scene_height) as int,
                        (0.45   * scene_width)  as int,
                        (0.45   * scene_height) as int,
                     );
                await Play(&s.scene, &shoes);
                emit resize(0, 0, scene_width, scene_height);
            end
        end
    end
end

escape 0;
