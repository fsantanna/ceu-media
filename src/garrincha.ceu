#include "media.ceu"

var Scene s with
  this.width = 1080;
  this.height = 720;
end;

var Rect frame = Rect (0.05 * s.width, 0.067 * s.height, 
                               0.185 * s.width, 0.185 * s.height);

var Media back = Media.IMAGE ("../../resources/background.png", 0, 0, s.width, 
                        s.height, 1, 1.0);
var Media anim = Media.VIDEO ("../../resources/animGar.mp4", 0, 0, s.width, 
                        s.height, 2, 1.0, 1.0);
var Media choro = Media.AUDIO ("../../resources/choro.mp4", 1.0);
var Media drible = Media.VIDEO ("../../resources/drible.mp4", frame.x, frame.y, 
                        frame.width, frame.height, 3, 1.0, 1.0);
var Media photo = Media.IMAGE ("../../resources/photo.png", frame.x, frame.y, 
                        frame.width, frame.height, 3, 0.6);
var Media icon = Media.IMAGE ("../../resources/icon.png", 
                        0.875 * s.width, 0.117 * s.height, 
                        0.0845 * s.width, 0.067 * s.height, 
                        3, 1.0);
var Media shoes = Media.VIDEO ("../../resources/shoes.mp4", 
                        0.15 * s.width, 0.6 * s.height, 
                        0.25 * s.width, 0.25 * s.height, 
                        3, 1.0, 1.0);

event (void) finish;
event (int, int, int, int) resize;

watching finish do
  par/and do
    do Player.play (back, &s);
  with
    var Player animPlayer = Player.play (anim, &s);
    watching animPlayer do
      every (x, y, width, height) in resize do
        animPlayer.setBounds (x, y, width, height);
      end
    end
    emit finish;
  with
    await 5s;
    do Player.play (choro, &s);
  with
    await 12s;
    do Player.play (drible, &s);
  with
    await 41s;
    var Player photoPlayer = Player.play (photo, &s);
    watching 5s do
      await 1s;

      var int top = photoPlayer.getPropertyInt ("y");
      var int diff = 290 - top;
      var int amount = 1;
      watching 3s do
        every (3000.0/diff)ms do
          photoPlayer.setPropertyInt ("y", top + amount);
          amount = amount + 1;
        end
      end
    end
  with
    await 45s;
    var bool flag = false;
    watching 6s do
      var Player iconPlayer = Player.play (icon, &s);
      var _charptr key;
      var int press;

      (key, press) = await s.key_event until _strcmp (key, "F1") == 0;
      flag = true;
    end
    if (flag) then
      emit resize => (0.05 * s.width, 0.0667 * s.height, 
                     0.45 * s.width, 0.45 * s.height);

      do Player.play (shoes, &s);
      emit resize => (0, 0, s.width, s.height);
    end
  end
end

escape 0;
