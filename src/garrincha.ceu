#include "media.ceu"

var Scene s with
  this.width = 1080;
  this.height = 720;
end;

var char [] backgroundUrl = [] .. "../../resources/background.png";
var Rect backgroundRect = Rect (0, 0, s.width, s.height);
var Media background with
  this.uri    = &backgroundUrl[0];
  this.scene  = &s;
  this.m      = &this.init();
  this.setRectBounds (backgroundRect);
  this.setPropertyInt ("z", 1);
end;

var char [] animUrl = [] .. "../../resources/animGar.mp4";
var Rect animRect = Rect (0, 0, s.width, s.height);
var Media anim with
  this.uri    = &animUrl[0];
  this.scene  = &s;
  this.m      = &this.init();
  this.setRectBounds (animRect);
  this.setPropertyInt ("z", 2);
end;

var Rect frameRect = Rect (0.05 * s.width, 0.067 * s.height, 
                               0.185 * s.width, 0.185 * s.height);

watching anim do
  par/and do
    await 5s;

    var char [] choroUrl = [] .. "../../resources/choro.mp4";
    var Media choro with
      this.uri    = &choroUrl[0];
      this.scene  = &s;
      this.m      = &this.init();
    end;
  with
    await 12s;

    var char [] dribleUrl = [] .. "../../resources/drible.mp4";
    var Media drible with
      this.uri    = &dribleUrl[0];
      this.scene  = &s;
      this.m      = &this.init ();
      this.setRectBounds (frameRect);
      this.setPropertyInt ("z", 3);
    end;
  with
    await 41s;
    var char [] photoUrl = [] .. "../../resources/photo.png";
    var Media photo with
      this.uri    = &photoUrl[0];
      this.scene  = &s;
      this.m      = &this.init ();
      this.setRectBounds (frameRect);
      this.setPropertyInt ("z", 3);
      this.setPropertyFloat ("alpha", 0.6);
    end;

    watching 5s do
      await 1s;

      var int top = photo.getPropertyInt ("y");
      var int diff = 290 - top;
      var int amount = 1;
      watching 3s do
        every (3000.0/diff)ms do
          photo.setPropertyInt ("y", top + amount);
          amount = amount + 1;
        end
      end
    end
  with
    await 45s;
    var bool flag = false;
    watching 6s do
      var char [] iconUrl = [] .. "../../resources/icon.png";
      var Media icon with
        this.uri    = &iconUrl[0];
        this.scene  = &s;
        this.m      = &this.init();
        this.setBounds (0.875 * s.width, 0.117 * s.height, 
                                     0.0845 * s.width, 0.067 * s.height);
        this.setPropertyInt ("z", 3);
      end;
      
      var _charptr key;
      var int press;

      (key, press) = await s.key_event until _strcmp (key, "F1") == 0;
      flag = true;
    end

    if (flag) then
      anim.setBounds (0.05 * s.width, 0.0667 * s.height, 
                         0.45 * s.width, 0.45 * s.height);

      var char [] shoesUrl = [] .. "../../resources/shoes.mp4";
      var Rect shoesRect = Rect (0.15 * s.width, 0.6 * s.height, 
                                     0.25 * s.width, 0.25 * s.height);
      var Media shoes with
        this.uri    = &shoesUrl[0];
        this.scene  = &s;
        this.m      = &this.init();
        this.setRectBounds (shoesRect);
        this.setPropertyInt ("z", 3);
      end;

      await shoes;
      animRect = Rect (0, 0, s.width, s.height);
      anim.setRectBounds (animRect);
    end
  with
    await anim;
  end
end

escape 0;
