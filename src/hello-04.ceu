#include "c.ceu"
#include "media/media.ceu"

var int width = 640;
var int height = 480;

var& IScene scene;
watching Scene(Size(width,height),true) => (scene)
do
    vector[] byte uri1 = [] .. "resources/animGar.mp4";
    var Media.Video back = val Media.Video (
                                    &uri1,
                                    Region(0, 0, 1, width, height),
                                    1.0,
                                    1.0,
                                );

    vector[] byte uri2 = [] .. "resources/diode.mp4";
    var Media.Video video = val Media.Video (
                                    &uri2,
                                    Region(0, 0, 2, 320, 240),
                                    0.75,
                                    1.0,
                                );

    vector[] byte uri3 = [] .. "resources/choro.mp4";
    var Media.Audio audio = val Media.Audio (&uri3, 0.5);

    spawn Play(&scene, &back);

    var& IPlay play_video;
    watching Play(&scene, &video) => (play_video) do
        par/or do
            var _char_ptr_ext key;
            (key,_) = await scene.ok_keyed until (_strcmp(key,"q") == 0);
        with
            loop do
                var int x = call Play_Get_Int(&play_video, "x");
                var int y = call Play_Get_Int(&play_video, "y");

                var _char_ptr_ext key;
                (key, _) = await scene.ok_keyed;
                if _strcmp (key, "Left") == 0 then
                    call Play_Set_Int(&play_video, "x", x-10);
                else/if _strcmp (key, "Right") == 0 then
                    call Play_Set_Int(&play_video, "x", x+10);
                else/if _strcmp (key, "Up") == 0 then
                    call Play_Set_Int(&play_video, "y", y-10);
                else/if _strcmp (key, "Down") == 0 then
                    call Play_Set_Int(&play_video, "y", y+10);
                end
            end
        with
            loop do
                var _char_ptr_ext key;
                var bool pressed;
                (key, pressed) = await scene.ok_keyed
                                 until pressed and _strcmp(key,"space")==0;
                spawn Play(&scene, &audio);
                (key, pressed) = await scene.ok_keyed
                                 until pressed and _strcmp (key,"space")==0;
            end
        end
    end
end

escape 0;
