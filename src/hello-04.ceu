#include "c.ceu"
#include "media/media.ceu"

var int width = 640;
var int height = 480;

var Media.Video back = val Media.Video (
                                [].."resources/animGar.mp4",
                                Region(0, 0, 1, width, height),
                                1.0,
                                1.0,
                            );

var Media.Video video = val Media.Video (
                                [].."resources/diode.mp4",
                                Region(0, 0, 2, 320, 240),
                                0.75,
                                1.0,
                            );

var Media.Audio audio = val Media.Audio (
                                [].."resources/choro.mp4",
                                0.5,
                            );

var&? Scene s = spawn Scene(Size(width,height),true);
watching s do
    spawn Play(&s.scene, &back);

    var&? Play p = spawn Play(&s.scene, &video);
    watching p do
        par/or do
            var _char_ptr_ext key;
            (key,_) = await s.scene.ok_keyed until (_strcmp(key,"q") == 0);
        with
            loop do
                var int x = call Play_Get_Int(&p.play, "x");
                var int y = call Play_Get_Int(&p.play, "y");

                var _char_ptr_ext key;
                (key, _) = await s.scene.ok_keyed;
                if _strcmp (key, "Left") == 0 then
                    call Play_Set_Int(&p.play, "x", x-10);
                else/if _strcmp (key, "Right") == 0 then
                    call Play_Set_Int(&p.play, "x", x+10);
                else/if _strcmp (key, "Up") == 0 then
                    call Play_Set_Int(&p.play, "y", y-10);
                else/if _strcmp (key, "Down") == 0 then
                    call Play_Set_Int(&p.play, "y", y+10);
                end
            end
        with
            loop do
                var _char_ptr_ext key;
                var bool pressed;
                (key, pressed) = await s.scene.ok_keyed
                                 until pressed and _strcmp(key,"space")==0;
                spawn Play(&s.scene, &audio);
                (key, pressed) = await s.scene.ok_keyed
                                 until pressed and _strcmp (key,"space")==0;
            end
        end
    end
end

escape 0;
